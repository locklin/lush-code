;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; A set of classes for hyper-documents and books
;; Yann LeCun, May 2002.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; this includes HTML-like text rendering as well as hyperlinks.

;; (libload "ogre")
(libload "ldoc/ltree")


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(df ldoc-feval l (each ((x l)) (eval x)))

(de ldoc-parprint _x
    (let ((para (apply concat _x))
	  (idx 1) 
	  (newidx t)
	  (word ()) )
      (while newidx
	(setq newidx (index " " para idx))
	(if (not newidx)
	    (setq word (mid para idx))	  
	  (setq word (mid para idx (- newidx idx)))
	  (setq idx (1+ newidx)) )
	(cond
	 ((< (+ (tab) (len word)) _rightside)
	  (printf " %s" word) )
	 ((<> word "")
	  (printf "\n")
	  (tab _leftside)
	  (printf "%s" word))))))

(de ldoc-evalprintf (l _parmode)
    (each ((x l))
	  (cond 
	   ((consp x) (eval x))
	   ((stringp x) 
	    (if _parmode
		(ldoc-parprint x)
	      (printf x) (printf "\n")))
	   (t (error "illegal ldoc")))))

(df ldoc-fevalprintf l (ldoc-evalprintf l t))
(df ldoc-noop l ())

;; rendering ldoc tag into html
(de ldoc-html-tag (tag l)
    (printf "<%s>" tag)
    (ldoc-evalprintf l t)
    (printf "</%s>\n" tag))

;; rendering ldoc tag into latex
(de ldoc-latex-tag (tag l)
    (flatten (list (concat "\\" tag "{") 
		   (all ((x l)) (eval x))
		   (concat "}"))))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; ldoc object: a single page, item, or section in a book

;; a doc code is a normal lisp expression where the following 
;; functions have a special meaning and are interpreted as formatting 
;; or markup tags:
;;  ldoc-hlink: include a hyperlink to another ldoc in the same
;;         book (identified by name).
;;  ldoc-br:   line break
;;  ldoc-font: change font
;;  ldoc-p:    delimitates paragraphs
;;  ldoc-u:    underline
;;  ldoc-b:    bold face
;;  ldoc-i:    italics
;;  ldoc-tt:   typewriter fixed-pitch font
;;  ldoc-center: center text
;;  ldoc-img:  insert image					; image
;;  ldoc-pre:  preformatted text
;;  ldoc-ul:   unstructured list
;;  ldoc-$:    LaTeX math
;;
;;  ldoc-h1:   large title
;;  ldoc-h2:   medium title
;;  ldoc-h3:   small title
;;  ldoc-code: displayed code
;;
;;  ldoc-example:  insert Lush example
;;  ldoc-if-html: insert if rendering to HTML
;;  ldoc-if-latex: insert if rendering to LaTeX
;;  ldoc-if-ogre: insert if rendering to Ogre (screen)
;;  ldoc-if-text: insert if rendering to terminal


(defclass ldoc object
  doc					; * the actual doc code
  name				        ; name/entry for display
  keywords				; keyword list
  title					; (redundant with name)
  hlinks				; alist of (names . ldocs) pairs
  description				; one-line description (optional)
  file					; file where the text resides
  type					; type of object (e.g. "file" "dmd")
  author				; author
  date					; date of last modification
)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; this is a base class for a bundle of functions that will
;; operate on ldocs (to parse them or to render them).
;; subclasses of this base class include parsers and renderers.
(defclass ldoc-func object
  ldoc-mode 

  ;; metadata
  ldoc-title					; set document title 
  ldoc-anchor				; set anchor name of document
  ldoc-describe				; set short description
  ldoc-keywords				; set keywords
  ldoc-file
  ldoc-author

  ldoc-hlink					; hyperlink to heading

  ;; low-level text formatting tags
  ldoc-br					; line break
  ldoc-font					; font
  ldoc-p					; paragraph
  ldoc-u					; underlined
  ldoc-b					; bold
  ldoc-i					; italic
  ldoc-tt					; typewriter font
  ldoc-center				; center
  ldoc-img					; image
  ldoc-pre					; preformatted
  ldoc-ul					; unstructured list
  ldoc-li					; list item
  ldoc-$					; LaTeX math

  ;; more text formatting
  ldoc-h1					; big title
  ldoc-h2 					; medium title
  ldoc-h3					; small title
  ldoc-code					; displayed code

  ;; special commands
  ldoc-if-html
  ldoc-if-latex
  ldoc-if-ogre
  ldoc-if-text
)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; ldoc renderer for HTML
(defclass ldoc-html ldoc-func
  (_leftside 0)
  (_rightside 72)
  (_parmode t)
  (title-font "<font color=#cc0000>")
  (text-font "<font color=#000000>")
  )

(defmethod ldoc-html render-header (doc)
  (let ((_n :doc:name)
	(_type :doc:type))
    (when _n (printf "%s<h2>%s \n" title-font _n))
    (when _type (printf "[%s]" _type))
    (printf "</h2></font>\n")
    (when :doc:file 
      (printf "File:<font color=#22cc22>%s</font><br>\n" :doc:file))
    (when :doc:author
      (printf "Author:<font color=#2222cc>%s</font><br>\n" :doc:author))
    (when :doc:date
      (printf "Date:<font color=#444444>%s</font><br>\n" :doc:date))
    (when :doc:description
      (printf "Description:<font color=#ee4444>%s</font><br>\n" :doc:description))
    (when :doc:keywords
      (printf "Keywords:<font color=#ee4444>%s</font><br>\n" :doc:keywords))
    (printf "<p>\n")))
  
  
(defmethod ldoc-html render (doc)
  (==> this render-header doc)
  (printf "%s\n" text-font)
  (eval :doc:doc)
  (printf "</font><p>\n"))


(defmethod ldoc-html ldoc-html ()
  (setq ldoc-mode 'html)
  (setq ldoc-if-html  (flambda l (ldoc-evalprintf l t)))
  (setq ldoc-if-latex ldoc-noop)
  (setq ldoc-if-ogre ldoc-noop)
  (setq ldoc-if-text ldoc-noop)
  (setq ldoc-br (flambda () (printf "<br>\n")))
  (setq ldoc-p  (flambda l (ldoc-evalprintf l t) (printf "\n<p>\n")))
  (setq ldoc-h1 (flambda l (ldoc-html-tag "h1" l)))
  (setq ldoc-h2 (flambda l (ldoc-html-tag "h2" l)))
  (setq ldoc-h3 (flambda l (ldoc-html-tag "h3" l)))
  (setq ldoc-u (flambda l (ldoc-html-tag "u" l)))
  (setq ldoc-b (flambda l (ldoc-html-tag "b" l)))
  (setq ldoc-i (flambda l (ldoc-html-tag "i" l)))
  (setq ldoc-tt (flambda l (ldoc-html-tag "tt" l)))
  (setq ldoc-pre (flambda l (ldoc-html-tag "pre" l)))
  (setq ldoc-code (flambda l (ldoc-html-tag "pre" l)))
  (setq ldoc-center (flambda l (ldoc-html-tag "center" l)))
  (setq ldoc-hlink 
    (flambda (h . l)
	     (printf "<a href=\"%s\">" (car (nth (eval h) :doc:hlinks)))
	     (ldoc-evalprintf l _parmode)
	     (printf "</a>\n")))
  (setq ldoc-font 
    (flambda (s c . l)
	     (printf "<font size=%s color=%s>" (eval s) (eval c))
	     (ldoc-evalprintf l _parmode)
	     (printf "</font>\n")))
  (setq ldoc-img (flambda (s)
    (printf "<br><center><img src=\"%s\" border=0></center><br>" (eval s))))
  (setq ldoc-ul (flambda l (ldoc-html-tag "ul" l)))
  (setq ldoc-li (flambda l (ldoc-html-tag "li" l)))
  )

(setq *ldoc-html* (new ldoc-html))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; ldoc renderer for LaTeX
(defclass ldoc-latex ldoc-func)


(defmethod ldoc-latex ldoc-latex ()
  (setq ldoc-mode 'latex)
  (setq ldoc-if-html (flambda l ()))
  (setq ldoc-if-latex (flambda l (flatten (all ((x l)) (sprintf (eval x))))))
  (setq ldoc-if-ogre (flambda l ()))
  (setq ldoc-if-text (flambda l ()))
  (setq ldoc-begin (flambda () ()))
  (setq ldoc-end (flambda () ()))
  (setq ldoc-head (flambda l ()))
  (setq ldoc-title (flambda l ()))
  (setq ldoc-keywords (flambda l ()))
  (setq ldoc-body (flambda l ()))
  (setq ldoc-br (flambda () "\\\\"))
  (setq ldoc-p (flambda () "\n\n"))
  (setq ldoc-h1 (flambda l (flatten (list "\n\n{\\Large\\b" (all ((x l) (eval x))) "}\n\n"))))
  (setq ldoc-h2 (flambda l (flatten (list "\n\n{\\large\\b" (all ((x l) (eval x))) "}\n\n"))))
  (setq ldoc-h3 (flambda l (flatten (list "\n\n{\\b" (all ((x l) (eval x))) "}\n\n"))))
  (setq ldoc-u (flambda l (ldoc-tex "u" l)))
  (setq ldoc-b (flambda l (flatten (list "{\\b" (all ((x l) (eval x))) "}"))))
  (setq ldoc-i (flambda l (flatten (list "{\\it" (all ((x l) (eval x))) "}"))))
  (setq ldoc-i (flambda l (flatten (list "{\\tt" (all ((x l) (eval x))) "}"))))
  (setq ldoc-pre (flambda l (flatten 
			(list "\\begin{verbatim}"
			      (all ((x l) (eval x)))
			      "\\end{verbatim}"))))
  (setq ldoc-center (flambda l (flatten 
			   (list "\\begin{center}"
				 (all ((x l) (eval x)))
				 "\\end{center}"))))
  (setq ldoc-hlink 
    (flambda (h . l)
      (flatten (list (all ((x l)) (eval x))
		     (sprintf "(see~\\ref{%s})" (eval h))))))
  (setq ldoc-name 
    (flambda (h . l)
      (flatten (list (sprintf "\\label{%s}" (eval h))))))
  (setq ldoc-font 
    (flambda (s c . l)
      (flatten (list (sprintf "{ (font %s %s)" (eval s) (eval c))
		     (all ((x l)) (eval x))
		     "}"))))
  (setq ldoc-img (flambda (s)
    (sprintf "\n\n\\begin{center}(image %s)\\end{center}\n\n" (eval s))))
  (setq ldoc-ul 
    (flambda l 
      (flatten (list "\\begin{itemize}" 
		     (all ((x l)) (list "\\item " (eval x)))
		     "\\end{itemize}"))))
  )


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; ldoc renderer for ogre display
(defclass ldoc-ogre ldoc-func
  mode
  font-name
  font-style
  font-size
  font-color
  ldoc-x				; current painting position
  ldoc-y
  ldoc-xx				; upper-left corner of drawing area
  ldoc-yy
  ldoc-width				; width/height of drawing area
  ldoc-height
)

(de ldoc-ogre-font (nam styl size col) ())


(defmethod ldoc-ogre ldoc-ogre ()
  (setq ldoc-mode 'ogre)
  (setq ldoc-begin (flambda l ()))
  (setq ldoc-end (flambda l ()))
  (setq ldoc-head (flambda l ()))
  (setq ldoc-title (flambda l ()))
  (setq ldoc-keywords (flambda l ()))
  (setq ldoc-body (flambda l ()))
  (setq ldoc-br (lambda () (setq ldoc-x ldoc-xx) (incr ldoc-y ldoc-lineh)))
  (setq ldoc-p (lambda () (setq ldoc-x ldoc-xx) (incr ldoc-y (* 2 ldoc-lineh))))
  (setq ldoc-h1 (flambda l 
	     (let ((fontbkp ldoc-font))
	       (font (setq ldoc-font "Helvetica-Bold-24"))
	       (ldoc-paint-eval l)
	       (font fontbkp))))
  (setq ldoc-h2 (flambda l 
	     (let ((fontbkp ldoc-font))
	       (font (setq ldoc-font "Helvetica-Bold-14"))
	       (ldoc-paint-eval l)
	       (font fontbkp))))
  (setq ldoc-h3 (flambda l 
	     (let ((fontbkp ldoc-font))
	       (font (setq ldoc-font "Helvetica-Bold-11"))
	       (ldoc-paint-eval l)
	       (font fontbkp))))
  (setq ldoc-u (flambda l 
	    (let ((x ldoc-x) (y (+ ldoc-y 3)))
	      (ldoc-paint-eval l)
	      (draw-line x y ldoc-x y))))
  (setq ldoc-b (flambda l 
	     (let ((fontbkp ldoc-font))
	       (font (setq ldoc-font (regex-subst "^.?-" "%0Bold-" ldoc-font)))
	       (ldoc-paint-eval l)
	       (font fontbkp))))
  (setq ldoc-i (flambda l (ldoc-add-tag "i" l)))
  (setq ldoc-tt (flambda l (ldoc-add-tag "tt" l)))
  (setq ldoc-pre (flambda l (ldoc-add-tag "pre" l)))
  (setq ldoc-center (flambda l (ldoc-add-tag "center" l)))
  (setq ldoc-hlink (flambda (h . l)
		(ldoc-add (sprintf "<a href=\"%s\">" (eval h)))
		(ldoc-add-eval l)
		(ldoc-add "</a>")))
  (setq ldoc-name (flambda (h . l)
		 (ldoc-add (sprintf "<a name=\"%s\">" (eval h)))
		 (ldoc-add-eval l)))
  (setq ldoc-font (flambda (c s . l)
               (ldoc-add (sprintf "<font size=%s color=%s>"))
	       (ldoc-add-eval l)
	       (ldoc-add "</font>")))
  (setq ldoc-img (lambda (s)
	      (ldoc-add (sprintf "<br><center><img src=\"%s\" border=0></center><br>" s))))
  (setq ldoc-ul (flambda l 
	     (ldoc-add "<ul>")
	     (all ((x l)) (ldoc-add "<li>") (ldoc-add (eval x)))
	     (ldoc-add "</ul>")))
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; ldoc renderer for text display
(defclass ldoc-text ldoc-func
  (_leftside 0)
  (_rightside 72)
  (_parmode t)
 )

(defmethod ldoc-text render-header (doc)
  (let ((_n :doc:name)
	(_type :doc:type))
    (repeat 6 (printf "------------")) (printf "\n")
    (when _n
      (printf "%s" _n)
      (repeat (max 1 (- 40 (len _n))) (printf " "))
      (if (not _type)
	  (repeat 8 (printf " "))
	(repeat (max 1 (- 8 (len _type))) (printf " "))
	(printf "[%s]" _type))
      (when (and :doc:file (<> (right :doc:file 4) ".hlp"))
	(printf "(%s)" :doc:file))
      (printf "\n")
      (repeat 6 (printf ". . . . . . ")) (printf "\n")))
  (when :doc:author (printf "Author(s): %s\n" :doc:author))
  (when :doc:date (printf "Date:%s\n" :doc:date))
  (when :doc:description (printf "Description: %s\n" :doc:description))
  (when :doc:keywords (printf "Keywords: %s\n" :doc:keywords))
  (printf "\n"))
  
(defmethod ldoc-text render (doc)
  (setq _parmode t)
  (==> this render-header doc)
  (eval :doc:doc)
  (printf "\n"))


(defmethod ldoc-text ldoc-text ()
  (setq ldoc-mode 'text)
  (setq ldoc-if-html ldoc-noop)
  (setq ldoc-if-latex ldoc-noop)
  (setq ldoc-if-ogre ldoc-noop)
  (setq ldoc-if-text (flambda l (ldoc-evalprintf l t)))
  (setq ldoc-br (flambda () (printf "\n")))
  (setq ldoc-p  (flambda l (ldoc-evalprintf l t) (printf "\n")))
  (setq ldoc-h1 (flambda l (let ((_leftside (+ _leftside 4)))
			(printf "*** ") (ldoc-evalprintf l t) (printf "\n"))))
  (setq ldoc-h2 (flambda l (let ((_leftside (+ _leftside 3)))
			(printf "** ") (ldoc-evalprintf l t) (printf "\n"))))
  (setq ldoc-h3 (flambda l (let ((_leftside (+ _leftside 2)))
			(printf "* ") (ldoc-evalprintf l t) (printf "\n"))))
  (setq ldoc-u  (flambda l (ldoc-evalprintf l _parmode)))
  (setq ldoc-b (flambda l (ldoc-evalprintf l _parmode)))
  (setq ldoc-i (flambda l (ldoc-evalprintf l _parmode)))
  (setq ldoc-tt (flambda l (ldoc-evalprintf l _parmode)))
  (setq ldoc-pre (flambda l (ldoc-evalprintf l ())))
  (setq ldoc-code (flambda l (ldoc-evalprintf l ())))
  (setq ldoc-center (flambda l (ldoc-evalprintf l ())))
  (setq ldoc-hlink 
    (flambda (h . l) (ldoc-evalprintf l _parmode)))
  (setq ldoc-font 
    (flambda (s c . l)  (ldoc-evalprintf l _parmode)))
  (setq ldoc-img (flambda (s) (printf "\n   [IMAGE %s]\n" (eval s))))
  (setq ldoc-ul (flambda l (ldoc-evalprintf l ())))
  (setq ldoc-li (flambda l 
		    (ldoc-evalprintf '("*") t)
		    (let ((_leftside (+ _leftside 3))) 
			(ldoc-evalprintf l t)
			(printf "\n"))))
  )

(setq *ldoc-text* (new ldoc-text))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#? doc
;;  {title}this is my title{/title}
;;  {keywords "stuff" "more-stuff"}
;;  {body} 					; body tag
;;  {head}
;;  {hlink node-name [anchor-name]}
;;  {a "anchor-name"}
;;  {br}
;;  {font size color}
;;  {p}
;;  {h1 "title"}
;;  {h2 "title"}
;;  {h3 "title"}
;;  {u "asdasd" "asd "}
;;  {b "asdasd" "asd "}
;;  {i "asdasd" "asd "}
;;  {tt "äsdasd" "asd "} (also <asdasd>)
;;  {center "asdasd"}
;;  {img "image-file.xxx"}
;;  {pre} {/pre} preformatted text
;;  {code} {/code} lisp code
;;  {ul} {/ul}  unstructured list
;;  {li "asd asd ad"} list item
;;  $  $ LaTeX math
    

