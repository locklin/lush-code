;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; A set of classes for hyper-documents and books
;; Yann LeCun, May 2002.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; this includes HTML-like text rendering as well as hyperlinks.

;; (libload "ogre")
(libload "ldoc/ltree")


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(df ldoc-feval l (each ((x l)) (eval x)))

(de ldoc-parprint _x
    (let ((para (apply sprintf _x))
	  (idx 1) 
	  (newidx t)
	  (word ()) )
      (while newidx
	(setq newidx (index " " para idx))
	(if (not newidx)
	    (setq word (mid para idx))	  
	  (setq word (mid para idx (- newidx idx)))
	  (setq idx (1+ newidx)) )
	(cond
	 ((< (+ (tab) (len word)) _rightside)
	  (printf " %s" word) )
	 ((<> word "")
	  (printf "\n")
	  (tab _leftside)
	  (printf "%s" word))))))

(de ldoc-evalprintf (l _parmode)
    (each ((x l))
	  (cond 
	   ((consp x) (eval x))
	   ((stringp x) 
	    (if _parmode
		(ldoc-parprint x)
	      (printf x) (printf "\n")))
	   (t (error "illegal ldoc")))))

(df ldoc-fevalprintf l (ldoc-evalprintf l t))
(df ldoc-noop l ())

;; rendering ldoc tag into html
(de ldoc-html-tag (tag l)
    (printf "<%s>" tag)
    (ldoc-evalprintf l t)
    (printf "</%s>\n" tag))

;; rendering ldoc tag into latex
(de ldoc-latex-tag (tag l)
    (flatten (list (concat "\\" tag "{") 
		   (all ((x l)) (eval x))
		   (concat "}"))))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; ldoc object: a single page, item, or section in a book

;; a doc code is a normal lisp expression where the following 
;; functions have a special meaning and are interpreted as formatting 
;; or markup tags:
;;  hlink: include a hyperlink to another ldoc in the same
;;         book (identified by name).
;;  br:   line break
;;  font: change font
;;  p:    delimitates paragraphs
;;  u:    underline
;;  b:    bold face
;;  i:    italics
;;  tt:   typewriter fixed-pitch font
;;  center: center text
;;  img:  insert image					; image
;;  pre:  preformatted text
;;  ul:   unstructured list
;;  $:    LaTeX math
;;
;;  h1:   large title
;;  h2:   medium title
;;  h3:   small title
;;  code: displayed code
;;
;;  example:  insert Lush example
;;  if-html: insert if rendering to HTML
;;  if-latex: insert if rendering to LaTeX
;;  if-ogre: insert if rendering to Ogre (screen)
;;  if-text: insert if rendering to terminal


(defclass ldoc object
  doc					; * the actual doc code
  name				        ; name/entry for display
  keywords				; keyword list
  title					; (redundant with name)
  hlinks				; alist of (names . ldocs) pairs
  description				; one-line description (optional)
  file					; file where the text resides
  type					; type of object (e.g. "file" "dmd")
  author				; author
  date					; date of last modification
)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; this is a base class for a bundle of functions that will
;; operate on ldocs (to parse them or to render them).
;; subclasses of this base class include parsers and renderers.
(defclass ldoc-func object
  mode 

  ;; metadata
  title					; set document title 
  anchor				; set anchor name of document
  describe				; set short description
  keywords				; set keywords
  file
  author

  hlink					; hyperlink to heading

  ;; low-level text formatting tags
  br					; line break
  font					; font
  p					; paragraph
  u					; underlined
  b					; bold
  i					; italic
  tt					; typewriter font
  center				; center
  img					; image
  pre					; preformatted
  ul					; unstructured list
  li					; list item
  $					; LaTeX math

  ;; more text formatting
  h1					; big title
  h2 					; medium title
  h3					; small title
  code					; displayed code

  ;; special commands
  if-html
  if-latex
  if-ogre
  if-text
)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; ldoc renderer for HTML
(defclass ldoc-html ldoc-func
  (_leftside 0)
  (_rightside 72)
  (_parmode t)
  (title-font "<font color=#cc0000>")
  (text-font "<font color=#000000>")
  )

(defmethod ldoc-html render-header (doc)
  (let ((_n :doc:name)
	(_type :doc:type))
    (when _n (printf "%s<h2>%s \n" title-font _n))
    (when _type (printf "[%s]" _type))
    (printf "</h2></font>\n")
    (when :doc:file 
      (printf "File:<font color=#22cc22>%s</font><br>\n" :doc:file))
    (when :doc:author
      (printf "Author:<font color=#2222cc>%s</font><br>\n" :doc:author))
    (when :doc:date
      (printf "Date:<font color=#444444>%s</font><br>\n" :doc:date))
    (when :doc:description
      (printf "Description:<font color=#ee4444>%s</font><br>\n" :doc:description))
    (when :doc:keywords
      (printf "Keywords:<font color=#ee4444>%s</font><br>\n" :doc:keywords))
    (printf "<p>\n")))
  
  
(defmethod ldoc-html render (doc)
  (==> this render-header doc)
  (printf "%s\n" text-font)
  (eval :doc:doc)
  (printf "</font><p>\n"))


(defmethod ldoc-html ldoc-html ()
  (setq mode 'html)
  (setq if-html  (flambda l (ldoc-evalprintf l t)))
  (setq if-latex ldoc-noop)
  (setq if-ogre ldoc-noop)
  (setq if-text ldoc-noop)
  (setq br (flambda () (printf "<br>\n")))
  (setq p  (flambda l (ldoc-evalprintf l t) (printf "\n<p>\n")))
  (setq h1 (flambda l (ldoc-html-tag "h1" l)))
  (setq h2 (flambda l (ldoc-html-tag "h2" l)))
  (setq h3 (flambda l (ldoc-html-tag "h3" l)))
  (setq u (flambda l (ldoc-html-tag "u" l)))
  (setq b (flambda l (ldoc-html-tag "b" l)))
  (setq i (flambda l (ldoc-html-tag "i" l)))
  (setq tt (flambda l (ldoc-html-tag "tt" l)))
  (setq pre (flambda l (ldoc-html-tag "pre" l)))
  (setq code (flambda l (ldoc-html-tag "pre" l)))
  (setq center (flambda l (ldoc-html-tag "center" l)))
  (setq hlink 
    (flambda (h . l)
	     (printf "<a href=\"%s\">" (car (nth (eval h) :doc:hlinks)))
	     (ldoc-evalprintf l _parmode)
	     (printf "</a>\n")))
  (setq font 
    (flambda (s c . l)
	     (printf "<font size=%s color=%s>" (eval s) (eval c))
	     (ldoc-evalprintf l _parmode)
	     (printf "</font>\n")))
  (setq img (flambda (s)
    (printf "<br><center><img src=\"%s\" border=0></center><br>" (eval s))))
  (setq ul (flambda l (ldoc-html-tag "ul" l)))
  (setq li (flambda l (ldoc-html-tag "li" l)))
  )

(setq *ldoc-html* (new ldoc-html))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; ldoc renderer for LaTeX
(defclass ldoc-latex ldoc-func)


(defmethod ldoc-latex ldoc-latex ()
  (setq mode 'latex)
  (setq if-html (flambda l ()))
  (setq if-latex (flambda l (flatten (all ((x l)) (sprintf (eval x))))))
  (setq if-ogre (flambda l ()))
  (setq if-text (flambda l ()))
  (setq begin (flambda () ()))
  (setq end (flambda () ()))
  (setq head (flambda l ()))
  (setq title (flambda l ()))
  (setq keywords (flambda l ()))
  (setq body (flambda l ()))
  (setq br (flambda () "\\\\"))
  (setq p (flambda () "\n\n"))
  (setq h1 (flambda l (flatten (list "\n\n{\\Large\\b" (all ((x l) (eval x))) "}\n\n"))))
  (setq h2 (flambda l (flatten (list "\n\n{\\large\\b" (all ((x l) (eval x))) "}\n\n"))))
  (setq h3 (flambda l (flatten (list "\n\n{\\b" (all ((x l) (eval x))) "}\n\n"))))
  (setq u (flambda l (ldoc-tex "u" l)))
  (setq b (flambda l (flatten (list "{\\b" (all ((x l) (eval x))) "}"))))
  (setq i (flambda l (flatten (list "{\\it" (all ((x l) (eval x))) "}"))))
  (setq i (flambda l (flatten (list "{\\tt" (all ((x l) (eval x))) "}"))))
  (setq pre (flambda l (flatten 
			(list "\\begin{verbatim}"
			      (all ((x l) (eval x)))
			      "\\end{verbatim}"))))
  (setq center (flambda l (flatten 
			   (list "\\begin{center}"
				 (all ((x l) (eval x)))
				 "\\end{center}"))))
  (setq hlink 
    (flambda (h . l)
      (flatten (list (all ((x l)) (eval x))
		     (sprintf "(see~\\ref{%s})" (eval h))))))
  (setq name 
    (flambda (h . l)
      (flatten (list (sprintf "\\label{%s}" (eval h))))))
  (setq font 
    (flambda (s c . l)
      (flatten (list (sprintf "{ (font %s %s)" (eval s) (eval c))
		     (all ((x l)) (eval x))
		     "}"))))
  (setq img (flambda (s)
    (sprintf "\n\n\\begin{center}(image %s)\\end{center}\n\n" (eval s))))
  (setq ul 
    (flambda l 
      (flatten (list "\\begin{itemize}" 
		     (all ((x l)) (list "\\item " (eval x)))
		     "\\end{itemize}"))))
  )


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; ldoc renderer for ogre display
(defclass ldoc-ogre ldoc-func
  mode
  font-name
  font-style
  font-size
  font-color
  ldoc-x				; current painting position
  ldoc-y
  ldoc-xx				; upper-left corner of drawing area
  ldoc-yy
  ldoc-width				; width/height of drawing area
  ldoc-height
)

(de ldoc-ogre-font (nam styl size col) ())


(defmethod ldoc-ogre ldoc-ogre ()
  (setq mode 'ogre)
  (setq begin (flambda l ()))
  (setq end (flambda l ()))
  (setq head (flambda l ()))
  (setq title (flambda l ()))
  (setq keywords (flambda l ()))
  (setq body (flambda l ()))
  (setq br (lambda () (setq ldoc-x ldoc-xx) (incr ldoc-y ldoc-lineh)))
  (setq p (lambda () (setq ldoc-x ldoc-xx) (incr ldoc-y (* 2 ldoc-lineh))))
  (setq h1 (flambda l 
	     (let ((fontbkp ldoc-font))
	       (font (setq ldoc-font "Helvetica-Bold-24"))
	       (ldoc-paint-eval l)
	       (font fontbkp))))
  (setq h2 (flambda l 
	     (let ((fontbkp ldoc-font))
	       (font (setq ldoc-font "Helvetica-Bold-14"))
	       (ldoc-paint-eval l)
	       (font fontbkp))))
  (setq h3 (flambda l 
	     (let ((fontbkp ldoc-font))
	       (font (setq ldoc-font "Helvetica-Bold-11"))
	       (ldoc-paint-eval l)
	       (font fontbkp))))
  (setq u (flambda l 
	    (let ((x ldoc-x) (y (+ ldoc-y 3)))
	      (ldoc-paint-eval l)
	      (draw-line x y ldoc-x y))))
  (setq b (flambda l 
	     (let ((fontbkp ldoc-font))
	       (font (setq ldoc-font (regex-subst "^.?-" "%0Bold-" ldoc-font)))
	       (ldoc-paint-eval l)
	       (font fontbkp))))
  (setq i (flambda l (ldoc-add-tag "i" l)))
  (setq tt (flambda l (ldoc-add-tag "tt" l)))
  (setq pre (flambda l (ldoc-add-tag "pre" l)))
  (setq center (flambda l (ldoc-add-tag "center" l)))
  (setq hlink (flambda (h . l)
		(ldoc-add (sprintf "<a href=\"%s\">" (eval h)))
		(ldoc-add-eval l)
		(ldoc-add "</a>")))
  (setq name (flambda (h . l)
		 (ldoc-add (sprintf "<a name=\"%s\">" (eval h)))
		 (ldoc-add-eval l)))
  (setq font (flambda (c s . l)
               (ldoc-add (sprintf "<font size=%s color=%s>"))
	       (ldoc-add-eval l)
	       (ldoc-add "</font>")))
  (setq img (lambda (s)
	      (ldoc-add (sprintf "<br><center><img src=\"%s\" border=0></center><br>" s))))
  (setq ul (flambda l 
	     (ldoc-add "<ul>")
	     (all ((x l)) (ldoc-add "<li>") (ldoc-add (eval x)))
	     (ldoc-add "</ul>")))
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; ldoc renderer for text display
(defclass ldoc-text ldoc-func
  (_leftside 0)
  (_rightside 72)
  (_parmode t)
 )

(defmethod ldoc-text render-header (doc)
  (let ((_n :doc:name)
	(_type :doc:type))
    (repeat 6 (printf "------------")) (printf "\n")
    (when _n
      (printf "%s" _n)
      (repeat (max 1 (- 40 (len _n))) (printf " "))
      (if (not _type)
	  (repeat 8 (printf " "))
	(repeat (max 1 (- 8 (len _type))) (printf " "))
	(printf "[%s]" _type))
      (when (and :doc:file (<> (right :doc:file 4) ".hlp"))
	(printf "(%s)" :doc:file))
      (printf "\n")
      (repeat 6 (printf ". . . . . . ")) (printf "\n")))
  (when :doc:author (printf "Author(s): %s\n" :doc:author))
  (when :doc:date (printf "Date:%s\n" :doc:date))
  (when :doc:description (printf "Description: %s\n" :doc:description))
  (when :doc:keywords (printf "Keywords: %s\n" :doc:keywords))
  (printf "\n"))
  
(defmethod ldoc-text render (doc)
  (setq _parmode t)
  (==> this render-header doc)
  (eval :doc:doc)
  (printf "\n"))


(defmethod ldoc-text ldoc-text ()
  (setq mode 'text)
  (setq if-html ldoc-noop)
  (setq if-latex ldoc-noop)
  (setq if-ogre ldoc-noop)
  (setq if-text (flambda l (ldoc-evalprintf l t)))
  (setq br (flambda () (printf "\n")))
  (setq p  (flambda l (ldoc-evalprintf l t) (printf "\n")))
  (setq h1 (flambda l (let ((_leftside (+ _leftside 4)))
			(printf "*** ") (ldoc-evalprintf l t) (printf "\n"))))
  (setq h2 (flambda l (let ((_leftside (+ _leftside 3)))
			(printf "** ") (ldoc-evalprintf l t) (printf "\n"))))
  (setq h3 (flambda l (let ((_leftside (+ _leftside 2)))
			(printf "* ") (ldoc-evalprintf l t) (printf "\n"))))
  (setq u  (flambda l (ldoc-evalprintf l _parmode)))
  (setq b (flambda l (ldoc-evalprintf l _parmode)))
  (setq i (flambda l (ldoc-evalprintf l _parmode)))
  (setq tt (flambda l (ldoc-evalprintf l _parmode)))
  (setq pre (flambda l (ldoc-evalprintf l ())))
  (setq code (flambda l (ldoc-evalprintf l ())))
  (setq center (flambda l (ldoc-evalprintf l ())))
  (setq hlink 
    (flambda (h . l) (ldoc-evalprintf l _parmode)))
  (setq font 
    (flambda (s c . l)  (ldoc-evalprintf l _parmode)))
  (setq img (flambda (s) (printf "\n   [IMAGE %s]\n" (eval s))))
  (setq ul (flambda l (ldoc-evalprintf l ())))
  (setq li (flambda l 
		    (ldoc-evalprintf '("*") t)
		    (let ((_leftside (+ _leftside 3))) 
			(ldoc-evalprintf l t)
			(printf "\n"))))
  )

(setq *ldoc-text* (new ldoc-text))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#? doc
;;  {title}this is my title{/title}
;;  {keywords "stuff" "more-stuff"}
;;  {body} 					; body tag
;;  {head}
;;  {hlink node-name [anchor-name]}
;;  {a "anchor-name"}
;;  {br}
;;  {font size color}
;;  {p}
;;  {h1 "title"}
;;  {h2 "title"}
;;  {h3 "title"}
;;  {u "asdasd" "asd "}
;;  {b "asdasd" "asd "}
;;  {i "asdasd" "asd "}
;;  {tt "äsdasd" "asd "} (also <asdasd>)
;;  {center "asdasd"}
;;  {img "image-file.xxx"}
;;  {pre} {/pre} preformatted text
;;  {code} {/code} lisp code
;;  {ul} {/ul}  unstructured list
;;  {li "asd asd ad"} list item
;;  $  $ LaTeX math
    

