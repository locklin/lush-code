
(libload "ogre")

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defclass LdocPane Control 
  width height left-margin right-margin top-margin bottom-margin)

(defmethod LdocPane LdocPane (w h) 
  (setq left-margin 6)
  (setq right-margin 6)
  (setq top-margin 12)
  (setq bottom-margin 6)
  (setq width (- w (+ left-margin right-margin)))
  (setq height (- h (+ top-margin bottom-margin)))
  (==> this resize w h))

(defmethod LdocPane setdata (doc)
  (let ((c (==> doc render-ogre width)))
    (setq height (cadr (car c)))
    (setq data (cadr c))
    (==> this resize 
	 (+ left-margin right-margin width) 
	 (+ top-margin bottom-margin height)))
  (==> this expose rect))

(defmethod LdocPane backpaint()
  (let (((x y w h) rect))
    (fill-down-rect x y w h (when (not disabled) color-bg)) ) )

(defmethod LdocPane backpaint-bw() (==> this backpaint))

(defmethod LdocPane repaint-bw() (==> this repaint))

(defmethod LdocPane repaint()
  (let (((xx yy w h) rect))
    (gsave
     (let ((x (+ xx left-margin))
	   (y (+ yy top-margin)))
       (addclip (list (+ xx 2) (+ yy 2) (- w 4) (- h 4)))
       (eval data)))))


  





