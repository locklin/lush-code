AC_INIT(README)
AC_CONFIG_HEADER(include/config.h)
AC_CONFIG_AUX_DIR(config)
AC_PREFIX_DEFAULT(/usr/local)
AC_CANONICAL_HOST
dnl ----------------------------------------
dnl Arguments
dnl ----------------------------------------
AC_ARG_ENABLE(debug, [  --enable-debug          compile with option -g])
AC_ARG_WITH(compiler,[  --with-compiler=<cc>    use C compiler <cc>], CC=$withval)
dnl ----------------------------------------
dnl Directories
dnl ----------------------------------------
lushdir=lush/lush
AC_SUBST(lushdir)
dnl ----------------------------------------
dnl Compiler
dnl ----------------------------------------
AC_PROG_MAKE_SET
AC_PROG_CC
CFLAGS="-O" ; 
test x${GCC-no} = xyes && CFLAGS="-O2"
test ${enable_debug-no} = yes && test $ac_cv_prog_cc_g = yes && CFLAGS="-g"
AC_ISC_POSIX
AC_AIX
dnl ----------------------------------------
dnl Programs
dnl ----------------------------------------
AC_PROG_RANLIB
AC_PROG_LN_S
dnl ----------------------------------------
dnl Libraries
dnl ----------------------------------------
AC_PATH_XTRA
test "${no_x-no}" != yes && X_LIBS="$X_LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS"
AC_SUBST(X_LIBS)
AC_CHECK_LIB(dl,dlopen)
AC_CHECK_LIB(dld,shl_load)
AC_CHECK_LIB(m,sqrt)
dnl ----------------------------------------
dnl Header Files
dnl ----------------------------------------
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_HEADER_TIME
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(memory.h string.h strings.h)
AC_CHECK_HEADERS(unistd.h sys/time.h sys/mman.h)
AC_CHECK_HEADERS(dlfcn.h dl.h ieeefp.h fpu_control.h)
AC_CHECK_HEADERS(stropts.h sys/stropts.h sys/select.h sys/ttold.h)
dnl ----------------------------------------
dnl Functions
dnl ----------------------------------------
AC_CHECK_FUNCS(strchr memcpy sigaction sigvec strerror)
AC_CHECK_FUNCS(setpgrp getcwd waitpid)
AC_CHECK_FUNCS(connect gethostbyname rexec)
AC_CHECK_FUNCS(vfork ieee_handler fpsetmask __setfpucw)
AC_CHECK_FUNCS(tanh sincos dlopen sigsetjmp)
AC_FUNC_MMAP
AC_FUNC_STRFTIME
AC_FUNC_SETPGRP
dnl ----------------------------------------
dnl Types
dnl ----------------------------------------
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_TYPE_PID_T
dnl ----------------------------------------
dnl Process CFLAGS
dnl ----------------------------------------
AC_MSG_CHECKING(config database for compilation flags)
. $srcdir/config/cflags.sh
AC_MSG_RESULT(ok)
dnl ----------------------------------------
dnl Compilation of Shared Libraries
dnl ----------------------------------------
AC_MSG_CHECKING(config database for shlib info)
. $srcdir/config/shlib.sh
if test x${GCC-no} = xyes ; then
  cc_pic_flag=$gcc_pic_flag
  cc_exp_flag=$gcc_exp_flag
  cc_link_shlib=$gcc_link_shlib
fi
if test ${have_shlib-no} != no && test x"${command_link_shlib}" = x ; then
  command_link_shlib='${CC} ${cc_link_shlib} ${cc_exp_flag} -o'
fi
AC_SUBST(have_shlib)
AC_SUBST(cc_pic_flag)
AC_SUBST(cc_exp_flag)
AC_SUBST(cc_link_shlib)
AC_SUBST(command_link_shlib)
AC_SUBST(exe_suffix)
AC_SUBST(shlib_suffix)
AC_MSG_RESULT($have_shlib)
dnl ----------------------------------------
dnl End
dnl ----------------------------------------
AC_OUTPUT(Makefile 
          src/Makefile 
          lib/Makefile 
          help/Makefile
          include/autoconf.h )


